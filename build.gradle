// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.2'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}

subprojects {
    repositories {
        mavenCentral()
        jcenter()
//https://plus.google.com/109385828142935151413/posts/hF7W59uZ7rX
//        mavenLocal()
        maven {
            url "${System.env.HOME}/.m2/repository"
        }
    }

    project.plugins.whenPluginAdded { plugin ->
        if ("com.android.build.gradle.AppPlugin".equals(plugin.class.name)) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        } else if ("com.android.build.gradle.LibraryPlugin".equals(plugin.class.name)) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        }
    }
}

project.ext {
    compileSdkVersion = 25
    buildToolsVersion = '25.0.0'
    supportLibVersion = '25.0.1'
    playServiceVersion = '9.8.0'
    // see http://tools.android.com/tech-docs/new-build-system/tips#TOC-Improving-Build-Server-performance.
    // CIサーバなどでは -PdisablePreDex をつける。 preDexが省略されてビルドが速くなる
    preDexLibs = !project.hasProperty('disablePreDex')

    releaseProperties = loadProperties(file('play-release.properties'))
}

def loadProperties(File file) {
    Properties properties = new Properties();
    if (!file.exists()) {
        return properties;
    }
    file.withInputStream {
        properties.load(it)
    }
    return properties;
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}